/*
 * Copyright (c) 2015-2020, Virgil Security, Inc.
 *
 * Lead Maintainer: Virgil Security Inc. <support@virgilsecurity.com>
 *
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 *     (1) Redistributions of source code must retain the above copyright notice, this
 *     list of conditions and the following disclaimer.
 *
 *     (2) Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *
 *     (3) Neither the name of virgil nor the names of its
 *     contributors may be used to endorse or promote products derived from
 *     this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 * DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
 * FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
 * DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
 * SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
 * CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
 * OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.virgilsecurity.ratchet_android_tests'
    compileSdk rootProject.ext.androidOptions.compileSdkVersion

    defaultConfig {
        applicationId 'com.virgilsecurity.ratchet_android_tests'
        minSdk rootProject.ext.androidOptions.minSdkVersion
        targetSdk rootProject.ext.androidOptions.targetSdkVersion
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    // Internal
    androidTestImplementation project(':ratchet-android')

    // Kotlin
    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
    implementation "androidx.appcompat:appcompat:$versions.appCompat"

    // Test
    testImplementation "junit:junit:$versions.junitOld"
    androidTestImplementation "androidx.test:runner:$versions.testsRunner"
    androidTestImplementation "androidx.test.ext:junit:$versions.testsExtJunit"
    androidTestImplementation "androidx.test:rules:$versions.testsRules"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"

    androidTestImplementation "com.virgilsecurity:test-common:$versions.virgilTestCommon"
}

tasks.register('copyEnvFile', Copy) {
    def assetsFolder = file("$projectDir/src/androidTest/resources/testProperties")

    onlyIf {
        file("${rootProject.projectDir}/env.json").exists()
    }

    doFirst {
        if (!assetsFolder.exists()) {
            assetsFolder.mkdirs()
        }
    }

    from("${rootProject.projectDir}/env.json")
    into(assetsFolder)
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    if (name.toLowerCase().contains('androidtest')) {
        dependsOn(tasks.named('copyEnvFile'))
    }
}
