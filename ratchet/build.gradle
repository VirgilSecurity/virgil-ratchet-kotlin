/*
 * Copyright (c) 2015-2020, Virgil Security, Inc.
 *
 * Lead Maintainer: Virgil Security Inc. <support@virgilsecurity.com>
 *
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 *     (1) Redistributions of source code must retain the above copyright notice, this
 *     list of conditions and the following disclaimer.
 *
 *     (2) Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *
 *     (3) Neither the name of virgil nor the names of its
 *     contributors may be used to endorse or promote products derived from
 *     this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 * DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
 * FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
 * DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
 * SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
 * CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
 * OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    // Virgil SDK
    api "com.virgilsecurity.sdk:crypto:$versions.virgilSdk"
    api "com.virgilsecurity.sdk:sdk:$versions.virgilSdk"

    // Virgil Crypto
    api "com.virgilsecurity.crypto:ratchet:$versions.virgilCrypto"

    // Http
    implementation "com.google.code.gson:gson:$versions.gson"
    implementation "com.github.kittinunf.fuel:fuel:$versions.fuel"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.coroutines"

    // Test
    testImplementation "org.junit.jupiter:junit-jupiter-api:$versions.junit"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$versions.junit"
    testImplementation "com.virgilsecurity:test-common:$versions.virgilTestCommon"
}

tasks.test {
    useJUnitPlatform()
    systemProperties = System.properties
}

tasks.register('generateVersionVirgilAgent') {
    def versionFile = file("$buildDir/generated/com/virgilsecurity/ratchet/build/VirgilInfo.kt")
    outputs.file(versionFile)

    doLast {
        versionFile.parentFile.mkdirs()
        versionFile.text =
                """
package com.virgilsecurity.ratchet.build;

object VirgilInfo {
    const val VERSION = "$project.version"
}
"""
    }
}

sourceSets {
    main {
        kotlin {
            srcDirs = ["$buildDir/generated", 'src/main/kotlin']
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    dependsOn(tasks.named('generateVersionVirgilAgent'))
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

tasks.register('sourcesJar', Jar) {
    from(sourceSets.main.allSource)
    archiveClassifier = 'sources'
}

tasks.register('javadocJar', Jar) {
    dependsOn(tasks.named('dokkaHtml'))
    from(layout.buildDirectory.dir('dokka/html'))
    archiveClassifier = 'javadoc'
}

// Central bundle upload uses Maven repository layout; Gradle module metadata is unnecessary.
tasks.withType(org.gradle.api.publish.tasks.GenerateModuleMetadata).configureEach {
    enabled = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'ratchet'
            from(components.java)
            artifact(tasks.named('sourcesJar'))
            artifact(tasks.named('javadocJar'))

            pom {
                name = 'Virgil Double Ratchet Java/Kotlin SDK'
                description = 'Virgil Security is presenting an implementation of the [Double Ratchet](https://signal.org/docs/specifications/doubleratchet/) algorithm, which is used by parties to exchange encrypted messages based on a shared secret key. The implementation includes:\n' +
                        '- **Virgil Perfect Forward Secrecy (PFS) service** - a standalone web-service that is dedicated to managing one-time keys and long-time keys that are based on their Identity Public Keys (public keys that are contained in user cards published on Virgil Cards service);\n' +
                        '- **Ratchet SDK** - interacts with PFS service for publishing and managing one-time keys and long-time keys and interacts with Virgil Cards service for retrieving user\'s indentity cards which the OTK and LTK are based on. The parties derive new keys for every Double Ratchet message so that previous private keys cannot be calculated from new ones. The parties that participate in the communication also send Diffie-Hellman public values attached to their messages. The results of Diffie-Hellman calculations are mixed into the derived keys so that the new private keys cannot be calculated from the previous ones.'
                url = 'https://www.virgilsecurity.com/'
                licenses {
                    license {
                        name = 'Virgil Security, Inc. license'
                        url = 'https://github.com/VirgilSecurity/virgil-ratchet-kotlin/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'virgil-security-inc'
                        name = 'Virgil Security, Inc.'
                        email = 'support@virgilsecurity.com'
                        organizationUrl = 'https://www.virgilsecurity.com/'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/VirgilSecurity/virgil-ratchet-kotlin.git'
                    developerConnection = 'scm:git:git@github.com:VirgilSecurity/virgil-ratchet-kotlin.git'
                    url = 'https://github.com/VirgilSecurity/virgil-ratchet-kotlin'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'centralBundle'
            url = uri("${rootProject.buildDir}/central-bundle-repo")
        }
    }
}

signing {
    def signingKey = providers.gradleProperty('signingKey').orNull ?: System.getenv('signingKey')
    def signingPassword = providers.gradleProperty('signingPassword').orNull ?: System.getenv('signingPassword')

    if (signingKey != null && signingPassword != null) {
        useInMemoryPgpKeys(signingKey, signingPassword)
    }

    sign publishing.publications.mavenJava
}

tasks.withType(org.jetbrains.dokka.gradle.DokkaTask).configureEach {
    dokkaSourceSets.configureEach {
        reportUndocumented.set(false)
    }
}
